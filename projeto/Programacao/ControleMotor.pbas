program ControleMotor

dim passo     as integer  'Contador de passo e representa as bobinas.
dim borda     as boolean  'Detecção de borda para o sinal de pulso.
dim borda2    as boolean  'Detecção de borda para o sinal de direção.
dim borda3    as boolean  'Detecção de borda para o sinal de pulsos/revolução.
dim direcao   as boolean  'Define o sentido de contagem das bobinas.
dim revolucao as boolean  'Define como será o formato de onda:
                          'True --> 400 pulsos por revolução 0,9°.
                          'False -> 200 pulsos por revolução 1,8°.

sub procedure pulso()
    if revolucao = true then
       if direcao = false then
          inc(passo)
          if passo > 8 then passo=1 end if
       end if
       if direcao = true then
          dec(passo)
          if passo < 1 then passo=8 end if
       end if
       if passo=1 then PORTC=%00001001 end if
       if passo=2 then PORTC=%00001000 end if
       if passo=3 then PORTC=%00001100 end if
       if passo=4 then PORTC=%00000100 end if
       if passo=5 then PORTC=%00000110 end if
       if passo=6 then PORTC=%00000010 end if
       if passo=7 then PORTC=%00000011 end if
       if passo=8 then PORTC=%00000001 end if
    else
        if direcao = false then
           inc(passo)
           if passo > 4 then passo=1 end if
        end if
        if direcao = true then
           dec(passo)
           if passo < 1 then passo=4 end if
        end if
        if passo=1 then PORTC=%00001000 end if
        if passo=2 then PORTC=%00000100 end if
        if passo=3 then PORTC=%00000010 end if
        if passo=4 then PORTC=%00000001 end if
     end if
end sub

main:
TRISA=%11111111
TRISB=%01111111
TRISC=%00000000
INTCON=%00000000
ANSEL=%00000000
ANSELH=%00000000

PORTC=0
passo=0
borda=false
borda2=false
borda3=false
direcao=false
revolucao=true 'Quando true 400 pulsos por revolução
               'Se false 200 pulsos por revolução.

executa:
        '****************************************
        ' Controle de pulso.
        '****************************************
        if testbit(porta,0)=1 then
           if borda=false then
              borda=true
              pulso()
           end if
        end if
        if testbit(porta,0)=0 then borda=false end if

        '****************************************
        ' Pulso de mudança de direção.
        '****************************************
        if testbit(porta,1)=1 then
           if borda2=false then
              borda2=true
              if direcao = false then direcao=true else direcao=false end if
           end if
        end if
        if testbit(porta,1)=0 then borda2=false end if

        '*****************************************
        ' Pulso para alternar entre 400 ou 200 ppr
        '*****************************************
        if testbit(porta,2)=1 then
           if borda3=false then
              borda3=true
              if revolucao=false then
                 revolucao = true
              else
                  revolucao=false
                  if passo > 4 then passo=4 end if
              end if
           end if
        end if
        if testbit(porta,2)=0 then borda3=false end if
        
       'reset
       'if testbit(porta,3) = 1 then

       ' end if
        
        goto executa
end.