program ControleMotor

dim passo     as integer  'Contador de passo e representa as bobinas.
dim borda     as boolean  'Detecção de borda para o sinal de pulso para o motor 1.
dim borda2    as boolean  'Detecção de borda para o sinal de pulso para o motor 2.
dim borda3    as boolean  'Detecção de borda para o sinal de direção.
dim borda4    as boolean  'Detecção de borda para o sinal de pulsos/revolução.
dim dirAntHor  as boolean  'Define o sentido de contagem das bobinas.
dim revolucao as boolean  'Define como será o formato de onda:
                          'True --> 400 pulsos por revolução 0,9°.
                          'False -> 200 pulsos por revolução 1,8°.

sub procedure pulso(dim motor as integer, dim inverte as boolean)
    if revolucao = true then
       if inverte = false then
           dec(passo)
           if passo < 1 then passo=8 end if
        end if
        if inverte = true then
           inc(passo)
           if passo > 8 then passo=1 end if
        end if
       
       if motor = 1 then
          if passo=1 then PORTC=9 end if
          if passo=2 then PORTC=8 end if
          if passo=3 then PORTC=12 end if
          if passo=4 then PORTC=4 end if
          if passo=5 then PORTC=6 end if
          if passo=6 then PORTC=2 end if
          if passo=7 then PORTC=3 end if
          if passo=8 then PORTC=1 end if
       end if
       
       if motor = 2 then
          if passo=1 then PORTC=144 end if
          if passo=2 then PORTC=128 end if
          if passo=3 then PORTC=192 end if
          if passo=4 then PORTC=64 end if
          if passo=5 then PORTC=96 end if
          if passo=6 then PORTC=32 end if
          if passo=7 then PORTC=48 end if
          if passo=8 then PORTC=16 end if
       end if

    else
        if inverte = false then
           inc(passo)
           if passo > 4 then passo=1 end if
        end if
        if inverte = true then
           dec(passo)
           if passo < 1 then passo=4 end if
        end if
        
        if motor = 1 then
           if passo=1 then PORTC=8 end if
           if passo=2 then PORTC=4 end if
           if passo=3 then PORTC=2 end if
           if passo=4 then PORTC=1 end if
        end if
        
        if motor = 2 then
           if passo=1 then PORTC=128 end if
           if passo=2 then PORTC=64  end if
           if passo=3 then PORTC=32  end if
           if passo=4 then PORTC=16  end if
        end if

     end if
end sub

main:
TRISA=%11111111
TRISB=%01111111
TRISC=%00000000
INTCON=%00000000
ANSEL=%00000000
ANSELH=%00000000

PORTC=0

passo=0
borda=false
borda2=false
borda3=false
borda4=false
dirAntHor=false
revolucao=true 'Quando true 400 pulsos por revolução
               'Quando false 200 pulsos por revolução.

executa:

        '****************************************
        ' Controle de pulso do Motor 1
        '****************************************
        if testbit(porta,0)=1 then
           if borda=false then
              borda=true
              pulso(1, dirAntHor)
           end if
        end if
        if testbit(porta,0)=0 then borda=false end if
        
        '****************************************
        ' Controle de pulso do Motor 2
        '****************************************
        if testbit(porta,1)=1 then
           if borda2=false then
              borda2=true
              pulso(2, dirAntHor)
           end if
        end if
        if testbit(porta,1)=0 then borda2=false end if
        
        '****************************************
        ' Pulso de mudança de direção.
        '****************************************
        if testbit(porta,2)=1 then
           dirAntHor = true
        else
            dirAntHor = false
        end if

        '*****************************************
        ' Pulso para alternar entre 400 ou 200 ppr
        '*****************************************
        if testbit(porta,3)=1 then
           if borda4=false then
              borda4=true
              if revolucao=false then
                 revolucao = true
              else
                  revolucao=false
                  if passo > 4 then passo=4 end if
              end if
           end if
        end if
        if testbit(porta,3)=0 then borda4=false end if
        
       'reset
       'if testbit(porta,3) = 1 then

       ' end if
        
        goto executa
end.